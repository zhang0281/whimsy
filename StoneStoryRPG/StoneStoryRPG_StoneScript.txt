// 意念之石启动之后,可以持续执行它所收到的指令.
// 指令必须使用写入石之脚本.
// 其他信息请见:StoneStoryRPG.com/stonescript.
// ——贤者贝赛拉

/*Poison (∞) in the Caves of Fear and the Temple
Vigor  (♥) in the Mushroom Forest
Æther  (*) in the Haunted Halls
Fire   (φ) in the Bronze Mines
Ice    (❄) in the Icy Ridge
恐怖洞窟 & 神庙 中的 毒药(∞)
蘑菇森林 里的 活力(♥)
亡者之殿 中的 以太 (*)
灼热矿井 中的 火 (φ)
不融山中 的 冰 (❄)*/

//import Cosmetics/Hats/Skully
import Cosmetics/PetBoo
import Fishing
import UI/BetterInfo2

/** 预设变量 **/
?loc.begin
  var freeChestTracker =
  ^import UI/FreeChestTracker
  var myUtils = new UI/OkamiroyUtils
  var MAIN_LO = 2
  var HEAL_LO = 3
  var MAGIC_LO = 4
  var PHY_LO = 1

  var LOW_HP_PERSENT = 0.65
  var needHealToMaxHp = false

  var debug = false

  var adaptiveDef
  var ghostBossFlag = false
  var timer = 3*30
  var timer2 = 7*30

?loc.begin | loc.loop
  ghostBossFlag = false
  timer = 3*30
  timer2 = 7*30

// debug = true

/** 在屏幕指定位置输出消息
 *  posY:屏幕Y轴坐标
 *  message 需要输出的消息,可以是变量名
 **/
func Print(posY,message)
  >`1,@posY@,#ffc57f,@message@
/** 拆分字符串
 *  当信息超出一行的时候拆分成两行显示
**/
func breakString(posY,s,atS)
  ?s
    var a = string.Break(s, 60)
    ? a.Count() = 1
        >`1,@posY@,#ffc57f,@atS@:@a［0］@
    :
      for i = 0 .. a.Count()-1
        >`1,@posY+i@,#ffc57f,@atS@_@i+1@:@a［i］@

/** 获取弱点 **/
func getWeakness()
  ?foe = ＂Vigor＂
    return ＂Poison＂
  ?foe = ＂AEther＂
    return ＂Vigor＂
  ?foe = ＂Fire＂
    return ＂AEther＂
  ?foe = ＂ Ice＂
    return ＂Fire＂
  ?foe = ＂ Poison＂
    return ＂Ice＂
  return false
/** 装备针对当前敌人弱点的wand(魔杖) **/
func equipWeakness()
  equipL wand @getWeakness()@ *10 +21
  equipR shield ah
/** 根据传入的bool型变量返回指定颜色
 *  bool: 指定bool类型变量
**/
func getColor(bool)
  ?bool = true
    return ＂#37FF8B＂
  :
    return ＂#B5446E＂
/** 在循环后自动回满HP **/
func loopHealHP(healLO)
  ?hp <= maxhp * LOW_HP_PERSENT
    loadout @healLO@
  ?foe = boss
    needHealToMaxHp = true
  ?needHealToMaxHp = true &
  ^hp <= maxhp &
  ^foe ! boss
    loadout @healLO@
    ?hp = maxhp
      needHealToMaxHp = false
/** 在屏幕上显示foe的debuff详情 **/
func showDebuffDetail()
  ?foe.debuffs.string
    var debuffsArr =
    ^string.Split(foe.debuffs.string,
    ^＂,＂,true)
    for i = 0 .. debuffsArr.Count()-1
      var debuffArr =
      ^string.Split(debuffsArr［i］,＂:＂,true)
      var e = debuffArr［0］
      var eName = debuffArr［1］
      var eLevel = debuffArr［2］
      var eTime = debuffArr［3］
      breakString(10+i,eLevel + ＂ ＂
      ^ + eTime,eName)
      >`@i*23+1@,1,#ffc57f,＂element＂:@e@
      >`@i*23+1@,2,#ffc57f,＂eName＂:@eName@
      >`@i*23+1@,3,#ffc57f,＂eLevel＂
      ^:@eLevel@
      >`@i*23+1@,4,#ffc57f,＂eTime＂:@eTime@

/** 岩石高地
 *  自动铲石头
 *  mainLO: 主Loadout
 *  healLO: 治疗Loadout
**/
func levelRocky(mainLO,healLO)
  ?loc = rocky
    ?foe = phase3
      LOW_HP_PERSENT = 0.95
    equipWeakness()
    // 自动铲石头
    ?harvest = Boulder &
    ^ harvest.distance < 30
      equip shovel
    ?foe = boss &
      ^getWeakness() ! false &
      ^string.IndexOf(foe.buffs.string,
      ^＂buff_protection＂) = -1
      ?item.GetCooldown(＂shield dash *10＂) <= 0
        equipR shield dash *10
        ?item.CanActivate(＂shield dash *10＂)
          activate R
    ?foe.buffs.string
      adaptiveDef = foe.buffs.string
      adaptiveDef = string.Split(adaptiveDef,＂:＂,true)［1］
      adaptiveDef = string.Split(adaptiveDef,＂adaptive_defense_＂,true)［0］
    ?adaptiveDef
      ?adaptiveDef = Poison
        equipL wand Vigor *10 +21
      ?adaptiveDef = Vigor
        equipL wand AEther *10 +21
      ?adaptiveDef = AEther
        equipL wand Fire *10 +21
      ?adaptiveDef = Fire
        equipL wand Ice *10 +21
      ?adaptiveDef = Ice
        equipL wand Poison *10 +21
      equipR shield ah
    ?foe.state = 115 & foe.time >= 60
      equipL mind stone
    ?hp <= maxhp * LOW_HP_PERSENT
      loadout @healLO@
/** 枯木峡谷
 *  自动砍树
 *  mainLO: 主Loadout
 *  healLO: 治疗Loadout
 *  phyLO: 物理伤害Loadout
**/
func levelDeadwood(mainLO,healLO,phyLO)
  ?loc = deadwood
    equipWeakness()
    loopHealHP(healLO)
    // 魔免怪加载近战
    ?foe = immune_to_ranged
      loadout @phyLO@
    // 自动砍树
    ?harvest = Tree
      equip hatchet
    ?foe = wasp
      equipL wand ice
      equipR shield ah
    ?foe = boss & foe = phase2
      equipL war hammer *10 +21
      equipR sword Vigor *10 +21
/** 恐怖洞窟
 *  在打完boss之后补满血
 *  mainLO: 主Loadout
 *  healLO: 治疗Loadout
**/
func levelCaves(mainLO,healLO)
  ?loc = caves
    loadout @mainLO@
    loopHealHP(healLO)
    ?foe = tiny_spider
      equipWeakness()
/** 蘑菇森林
 *  mainLO: 主Loadout
 *  healLO: 治疗Loadout
**/
func levelForest(mainLO,healLO)
  ?loc = forest
    loadout @mainLO@
    loopHealHP(healLO)
    ?foe = ant
      equipWeakness()
/** 亡者之殿
 *  mainLO: 主Loadout
 *  healLO: 治疗Loadout
 *  magicLO: 魔法伤害Loadout
 *  ? *>15 : use crossbow 3sec.
**/
func levelHalls(mainLO,healLO)
  ?loc = halls
    loadout @mainLO@
    loopHealHP(healLO)
    ?foe = immune_to_physical
      equipL wand Vigor
      equipR shield ah
    /*
    如果是召唤boss,用3秒的冰驽
    如果还在打large_ghost,等7s,如果还是large_ghost,再来3s
    */
    ?foe = ghost_tomb &
      ^ foe = boss &
      ^ foe = spawner
      timer = 3*30
      ghostBossFlag = true

    ?timer > 0 & ghostBossFlag = true
      equip crossbow ice D *10 +21
      timer--
    :
      timer = 3*30
      ghostBossFlag = false
      ?foe = large_ghost
        ?timer2 > 0 & ghostBossFlag = false
          timer2--
        :
          timer2 = 7*30
          ?foe = large_ghost
            ghostBossFlag = true
    ?debug = true
      >`1,8,@getColor(ghostBossFlag)@,timer:@timer@,timer2:@timer2@

  ?loc = undead_crypt_boss_harder
    equipL wand Vigor
    equipR sword Vigor *10 +21

/** 灼热矿井
 *  mainLO: 主Loadout
 *  healLO: 治疗Loadout
**/
func levelMine(mainLO,healLO)
  ?loc = mine
    equipWeakness()
    ?foe = boss
      loadout 5
    loopHealHP(healLO)
    ?(foe = fluff | foe = explode) &
    ^ foe.distance <= 5
      equipL mind stone
/** 不融山
 *  mainLO: 主Loadout
 *  healLO: 治疗Loadout
**/
func levelIcy(mainLO,healLO)
  ?loc = icy
    equipWeakness()
    loopHealHP(healLO)
/** 神庙
 *  mainLO: 主Loadout
 *  healLO: 治疗Loadout
 *  magicLO: 魔法伤害Loadout
**/
func levelTemple(mainLO,healLO,magicLO)
  ?loc = temple
    equipWeakness()
    loopHealHP(healLO)
    // 魔免怪加载近战
    ?foe = immune_to_Vigor
      loadout @magicLO@
    ?foe = flying_serpent
      equipWeakness()
/** 使用骷髅手臂
 *  在骷髅手臂可用,
 *  可以使用技能秒杀怪物
 *  非物理免疫,非boss
 *  不会干扰加速跑和自动捡取时
 *  装备并且使用骷髅手臂
**/
func steal()
  ?foe = boss & loc = deadwood & foe = phase2 |
    ^ loc = undead_crypt_boss_harder
    return false

  ?item.GetCooldown(＂skeleton_arm＂) <= 0 &
  ^(foe.hp + foe.armor) < 20 &
  ^foe ! immune_to_physical &
  ^foe ! boss &
  ^pickup.distance >= 20 &
  ^harvest.distance >= 20
    equip skeleton arm
    ?item.CanActivate(＂skeleton_arm＂)
       activate R
       >o-2,-3,#ffcccc,shhhh!~
/** 点击重置按钮之后重置
 *  blade_used_monsters
**/
func OnRstBtnPressed()
  var monsArr =
  ^string.Split(
  ^storage.Get(＂blade_used_monsters＂))
  monsArr.RemoveAt(monsArr.IndexOf(＂青铜守卫＂))
  monsArr.RemoveAt(monsArr.IndexOf(＂火元素＂))

  storage.Set(＂blade_used_monsters＂,
  ^string.Join(＂ ＂, monsArr))
  //storage.Delete(＂blade_used_monsters＂)

/** 添加重置按钮 **/
func addRstBtn()
  ?loc.begin
    var rstBtn = ui.AddButton()
    rstBtn.y = 1
    rstBtn.text = 删除指定元素
    rstBtn.SetPressed(OnRstBtnPressed)
/** 使用堕神大剑的主动能力 **/
func useBlade()
  ?foe = boss & loc = deadwood & foe = phase2 |
    ^ loc = undead_crypt_boss_harder
    return false

  ?item.GetCooldown(＂blade＂) <= 0 &
  ^foe.count > 5 &
  ^foe.distance <= 20 &
  ^pickup.distance >= 20 &
  ^harvest.distance >= 20
    equip blade
    ?item.CanActivate(＂blade＂)
      activate R
      >o-2,-3,#ffcccc,使用剑
      return true
  return false

/** 自动统计被堕神之剑的主动能力杀死的敌人
 *  用于完成 击败35类敌人 的任务
 *  由于无法获取全部敌人信息
 *  只能多次执行
**/
func bladeMission35kill()
  var monsArr
  var foeName
  monsArr = ［］
  foeName = foe.name

  ?foe &
  ^foe.distance <= 20
    ?storage.Has(＂blade_used_monsters＂)
      monsArr =
      ^string.Split(
      ^storage.Get(＂blade_used_monsters＂),true)
      ? ! monsArr.Contains(foeName)
        ?useBlade() = true
          monsArr.Add(foeName)
    :
      ?useBlade() = true
        monsArr.Add(foeName)
    ?monsArr ! ［］
      storage.Set(＂blade_used_monsters＂,
      ^string.Join(＂ ＂, monsArr))

  breakString(10,foe.name,@nowMon)
  breakString(11,
  ^storage.Get(＂blade_used_monsters＂),
  ^＂monsNum＂ + string.Split(
  ^storage.Get(＂blade_used_monsters＂)).Count()
  ^ + ＂/35＂)
/** 完成400000重击伤害任务 **/
func bladeMission400kDMG()
  ?(foe=boss & loc=deadwood & foe=phase2) |
    ^ hp <= maxhp * LOW_HP_PERSENT |
    ^ foe = immune_to_ranged |
    ^ loc = undead_crypt_boss_harder |
    ^ (loc=rocky & foe=boss |
    ^ foe.state = 115 & foe.time >= 60) |
    ^ ghostBossFlag = true |
    ^ needHealToMaxHp = true |
    ^string.IndexOf(foe.buffs.string,
    ^＂buff_protection＂) ! -1
    return false

  ?foe &
  ^pickup.distance >= 20 &
  ^harvest.distance >= 20
    var debuffStr = foe.debuffs.string
    ?string.IndexOf(debuffStr,
      ^＂debuff_damage:1＂) = -1&
      ^ foe ! immune_to_debuff_damage &
      ^ foe ! immune_to_ranged
      equipL wand poison
      equipR shield ah
    :?string.IndexOf(debuffStr,
      ^＂debuff_chill:6＂) = -1&
      ^ foe ! immune_to_debuff_chill &
      ^ foe ! immune_to_ranged
      equipL wand ice
      equipR shield ah
    :?string.IndexOf(debuffStr,
      ^＂debuff_dot:1＂) = -1&
      ^ foe ! immune_to_debuff_dot &
      ^ foe ! immune_to_ranged
      equipL wand fire
      equipR shield ah
    :?string.IndexOf(debuffStr,
      ^＂stun:1＂) = -1 &
      ^ foe ! immune_to_stun &
      ^ foe ! immune_to_physical &
      ^ loc ! mine & foe ! explode
      equipL hammer *10 +21
      equipR shield ah

    ?debug = true
      showDebuffDetail()
/** 使用面具的主动能力 **/
func useMask()
  ?foe = boss &
    ^(loc = temple)
    return false

  ?item.GetCooldown(＂mask＂) <= 0 &
  ^foe.count > 0 &
  ^foe.distance <= 20 &
  ^pickup.distance >= 20 &
  ^harvest.distance >= 20
    equip mask
    ?item.CanActivate(＂mask＂)
      activate R
      >o-2,-3,#ffcccc,使用面具
      return true
  return false
/** 自动拾取
 *  距离可拾取物品足够近的时候
 *  自动拾取物品
**/
func autoPickup()
  ?pickup & pickup.distance < 10
    equipL star_stone
    equipR shield ah
/** 血量小于7喝药 **/
func autoPotion()
  ?hp <= maxhp*0.3
    activate potion
/** 加速
 *  在离怪物,可拾取物,可收获物
 *  足够远的时候装备三曲腿加速跑
**/
func runFast()
  ?foe.distance > 20 &
  ^ pickup.distance >= 20 &
  ^ harvest.distance >= 20
    equipL triskelion stone
    equipR shield ah
/** 显示自定义变量的值 **/
func showInfo()
  ?loc = undead_crypt_intro |
    ^ loc = uulaa_shop |
    ^ loc = waterfall |
    ^ loc = deadwood_river |
    ^ loc = bronze_mine |
    ^ loc = cross_bridge
    return false

  ?debug = true
    breakString(9,loc,loc.name)
    breakString(11,foe,foe.name)

  >c-25,11,@getColor(hp <= maxhp * LOW_HP_PERSENT)@,@maxhp * LOW_HP_PERSENT@
  >c-42,11,@getColor(needHealToMaxHp)@,NeedHealToMaxHp


  myUtils.ShowTimer(10, 1)
  myUtils.ShowTimeStats(72, 1)
  myUtils.ShowFoesBar(20, 1, 30, true)
  myUtils.ShowPlayersBar(3, -5, 7)
/** 显示宝箱位置 **/
func freeChest()
  ?loc = undead_crypt_intro
    freeChestTracker.Main()

/** 执行对应关卡的策略 **/
levelRocky(MAIN_LO,HEAL_LO)
levelDeadwood(MAIN_LO,HEAL_LO,PHY_LO)
levelCaves(MAIN_LO,HEAL_LO)
levelForest(MAIN_LO,HEAL_LO)
levelHalls(MAIN_LO,HEAL_LO)
levelMine(MAIN_LO,HEAL_LO)
levelIcy(MAIN_LO,HEAL_LO)
levelTemple(MAIN_LO,HEAL_LO,MAGIC_LO)
/** 其他功能 **/
// 自动拾取
autoPickup()

//bladeMission35kill()
//addRstBtn()
bladeMission400kDMG()

// 偷取道具
steal()
// 堕神大剑相关
useBlade()

// 使用面具
useMask()
// 低血量回血
autoPotion()
// 加速跑
runFast()
// 宝箱小游戏提示
freeChest()
// 显示对应的提示信息
showInfo()
>`0,6,state @foe.state@time @foe.time@
/** 完毕 **/
