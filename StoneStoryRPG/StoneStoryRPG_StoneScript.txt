// 意念之石启动之后,可以持续执行它所收到的指令.
// 指令必须使用写入石之脚本.
// 其他信息请见:StoneStoryRPG.com/stonescript.
// ——贤者贝赛拉

/*Poison (∞) in the Caves of Fear and the Temple
Vigor  (♥) in the Mushroom Forest
Æther  (*) in the Haunted Halls
Fire   (φ) in the Bronze Mines
Ice    (❄) in the Icy Ridge
恐怖洞窟 & 神庙 中的 毒药(∞)
蘑菇森林 里的 活力(♥)
亡者之殿 中的 以太 (*)
灼热矿井 中的 火 (φ)
不融山中 的 冰 (❄)*/

//import Cosmetics/Hats/Skully
import Cosmetics/PetBoo
import Fishing
import UI/BetterInfo2

/** 预设变量 **/
?loc.begin
  var freeChestTracker =
  ^import UI/FreeChestTracker
  var myUtils = new UI/OkamiroyUtils

  var adaptiveDef

  var MAIN_LO = 2
  var HEAL_LO = 3
  var MAGIC_LO = 4
  var PHY_LO = 1

  var LOW_HP_PERSENT = 0.65
  var needHealToMaxHp = false

  var debug = false

  var ghostBossFlag = false
  var timer = 3*30
  var timer2 = 7*30
  var cdWindow = ascii
╔ CD显示  ╗
║冲撞盾    ║
║冲刺盾    ║
║思维石    ║
║骷髅手    ║
║堕神剑    ║
║面 具    ║
╚═══════╝
asciiend
  var dmgWindow = ascii
╔   DMG显示   ╗
║CurDPS:    ║
║DPSTms:    ║
║TtDMG :    ║
║AvgDPS:    ║
╚═══════════╝
asciiend

?loc.begin | loc.loop
  ghostBossFlag = false
  timer = 3*30
  timer2 = 7*30

  var updatespeed = 30
  var dpsTimer = 0

  var dps = 0
  var foeHPAndArmor = 0
  var averagedmg = 0
  var times = 0
  var totaldmg = 0
  
  var aacTimer = 0
  var DEFAULT_FRAME = 15
  var aacFoeHP = 0
  var changeLR = false

debug = false

/** 在屏幕指定位置输出消息
 *  posY:屏幕Y轴坐标
 *  message 需要输出的消息,可以是变量名
 **/
func Print(posY,message)
  >`1,@posY@,#ffc57f,@message@
/** 拆分字符串
 *  当信息超出一行的时候拆分成两行显示
**/
func breakString(posY,s,atS)
  ?s
    var a = string.Break(s, 60)
    ? a.Count() = 1
        >`1,@posY@,#ffc57f,@atS@:@a［0］@
    :
      for i = 0 .. a.Count()-1
        >`1,@posY+i@,#ffc57f,@atS@_@i+1@:@a［i］@

/** 获取弱点 **/
func getWeakness()
  ?foe = ＂Vigor＂
    return ＂Poison＂
  ?foe = ＂AEther＂
    return ＂Vigor＂
  ?foe = ＂Fire＂
    return ＂AEther＂
  ?foe = ＂ Ice＂
    return ＂Fire＂
  ?foe = ＂ Poison＂
    return ＂Ice＂
  return false
/** 装备针对当前敌人弱点的wand(魔杖) **/
func equipWeaknessWand()
  equipL wand @getWeakness()@ *10 +21
  equipR shield ah
/** 根据传入的bool型变量返回指定颜色
 *  bool: 指定bool类型变量
**/
func getColor(bool)
  ?bool = true
    return ＂#37FF8B＂
  :
    return ＂#B5446E＂
/** 在循环后自动回满HP **/
func loopHealHP(healLO)
  ?hp <= maxhp * LOW_HP_PERSENT
    loadout @healLO@
  ?foe = boss
    needHealToMaxHp = true
  ?needHealToMaxHp = true &
  ^hp <= maxhp &
  ^foe ! boss
    loadout @healLO@
    ?hp = maxhp
      needHealToMaxHp = false
/** 显示指定道具的冷却时间 **/
func getItemIsCanUse(posX, posY, itemStr)
  ?item.GetCooldown(itemStr) <=0
    >`@posX@,@posY@,
      ^@getColor(true)@,
      ^RDY
  :
    >`@posX@,@posY@,
      ^@getColor(false)@,
      ^@item.GetCooldown(itemStr) / 30@s
/** 显示CD相关信息 **/
func showCDWindow()
  >`19,1,#white,@cdWindow@
  getItemIsCanUse(24,2,＂bash＂)
  getItemIsCanUse(24,3,＂shield dash *10＂)
  getItemIsCanUse(24,4,＂mind stone＂)
  getItemIsCanUse(24,5,＂skeleton_arm＂)
  getItemIsCanUse(24,6,＂blade＂)
  getItemIsCanUse(24,7,＂mask＂)
/** 格式化数字输出 **/
func formatNumOutput(num)
  var result = num
  result = num
  ?string.Size(num+＂＂) > 4
    result = (num/1000)+k
  result = num
  ?string.Size(num+＂＂) > 4
    result = (num/1000)+m
  var spaceNum = 4-string.Size(result+＂＂)
  var spaceStr = ＂＂
  for i = 1..spaceNum
    spaceStr = spaceStr + ＂#＂
  return spaceStr+result
  // ?num > 999
  //   return num
  // :?num > 99
  //   return ＂#＂+num
  // :?num > 9
  //   return ＂##＂+num
  // :
  //   return ＂###＂+num
/** 显示DPS **/
func dpsChecker()
  ?dpsTimer <= updatespeed
    dpsTimer++
  :
    dpsTimer = 0
  
  ?dpsTimer = 0
    foeHPAndArmor = foe.hp + foe.armor
  
  ?dpsTimer = updatespeed
    dps = foeHPAndArmor - (foe.hp + foe.armor)

    ?dps > 0
      totaldmg = totaldmg + dps
      times ++

  ?dps < 0
    dps=0
  ?times > 0
    averagedmg = totaldmg / times

>`28,2,#white,@dmgWindow@
>`36,3,#ffffff,@formatNumOutput(dps)@
>`36,4,#ffffff,@formatNumOutput(times)@
>`36,5,#ffffff,@formatNumOutput(totaldmg)@
>`36,6,#ffffff,@formatNumOutput(averagedmg)@

/** 显示自定义变量的值 **/
func showInfo()
  ?loc = undead_crypt_intro |
    ^ loc = uulaa_shop |
    ^ loc = waterfall |
    ^ loc = deadwood_river |
    ^ loc = cross_bridge
    return false

  ?debug = true
    breakString(9,loc,loc.name)
    breakString(11,foe,foe.name)
    breakString(13,foe.buffs.string,＂buffs＂)
  
  showCDWindow()
  dpsChecker()

  >c-25,11,
    ^@getColor(hp <= maxhp * LOW_HP_PERSENT)@,
    ^@maxhp * LOW_HP_PERSENT@
  >c-42,11,
    ^@getColor(needHealToMaxHp)@,
    ^NeedHealToMaxHp

  myUtils.ShowTimer(10, 1)
  myUtils.ShowTimeStats(72, 1)
  myUtils.ShowFoesBar(30, 1, 25, true)
  myUtils.ShowPlayersBar(3, -5, 7)
/** 自动取消后摇 **/
func AAC(iL,iR)
  equipL @iL@
  equipR @iR@
  ?ai.idle
    equip skeleton
    equipL @iL@
    equipR @iR@
  // ?aacTimer <= DEFAULT_FRAME
  //   aacTimer++
  // :
  //   aacTimer = 0

  // ?aacTimer = 0
  //   changeLR = !changeLR
  //   aacFoeHP = foe.hp + foe.armor
  
  // ?aacTimer = DEFAULT_FRAME &
  // ^aacFoeHP > 0
  //   ?(foe.hp + foe.armor) < aacFoeHP
  //     DEFAULT_FRAME--
  //   :
  //     DEFAULT_FRAME++
  //     ?DEFAULT_FRAME > 35
  //       DEFAULT_FRAME = 15
        
  // >`0,18,aacTimer@aacTimer@\nDEFAULT_FRAME@DEFAULT_FRAME@ changeLR@changeLR@
  // ?debug = true
  //   >`0,20,@(foe.hp + foe.armor)@ aacFoeHP@aacFoeHP@ @(foe.hp + foe.armor) < aacFoeHP@
  //   >`0,21,L:@item.left@
  //   >`0,22,R:@item.right@
/** 使用骷髅手臂
 *  在骷髅手臂可用,
 *  可以使用技能秒杀怪物
 *  非物理免疫,非boss
 *  不会干扰加速跑和自动捡取时
 *  装备并且使用骷髅手臂
**/
func steal()
  ?(foe=boss & loc=deadwood & foe=phase2) |
  ^loc = undead_crypt_boss_harder
    return false

  ?item.GetCooldown(＂skeleton_arm＂) <= 0 &
  ^(foe.hp + foe.armor) < 20 &
  ^foe ! immune_to_physical &
  ^foe ! boss &
  ^pickup.distance >= 20 &
  ^harvest.distance >= 20
    equip skeleton arm
    ?item.CanActivate(＂skeleton_arm＂)
       activate R
       >o-2,-3,#ffcccc,shhhh!~
/** 点击重置按钮之后重置
 *  blade_used_monsters
**/
func OnRstBtnPressed()
  var monsArr = string.Split(
                  ^storage.Get(
                    ^＂blade_used_monsters＂
                  ^)
                ^)
  monsArr.RemoveAt(monsArr.IndexOf(＂青铜守卫＂))
  monsArr.RemoveAt(monsArr.IndexOf(＂火元素＂))

  storage.Set(
    ^＂blade_used_monsters＂,
    ^string.Join(＂ ＂, monsArr)
  ^)
  //storage.Delete(＂blade_used_monsters＂)

/** 添加重置按钮 **/
func addRstBtn()
  ?loc.begin
    var rstBtn = ui.AddButton()
    rstBtn.y = 1
    rstBtn.text = 删除指定元素
    rstBtn.SetPressed(OnRstBtnPressed)
/** 使用堕神大剑的主动能力 **/
func useBlade()
  ?(foe=boss & loc=deadwood & foe=phase2) |
  ^loc = undead_crypt_boss_harder
    return false

  ?item.GetCooldown(＂blade＂) <= 0 &
  ^foe.count > 5 &
  ^foe.distance <= 20 &
  ^pickup.distance >= 20 &
  ^harvest.distance >= 20
    equip blade
    ?item.CanActivate(＂blade＂)
      activate R
      >o-2,-3,#ffcccc,使用剑
      return true
  return false

/** 自动统计被堕神之剑的主动能力杀死的敌人
 *  用于完成 击败35类敌人 的任务
 *  由于无法获取全部敌人信息
 *  只能多次执行
**/
func bladeMission35kill()
  var monsArr
  var foeName
  monsArr = ［］
  foeName = foe.name

  ?foe & foe.distance <= 20
    ?storage.Has(＂blade_used_monsters＂)
      monsArr =
      ^string.Split(
        ^storage.Get(＂blade_used_monsters＂),
        ^true
      ^)
      ? ! monsArr.Contains(foeName)
        ?useBlade() = true
          monsArr.Add(foeName)
    :
      ?useBlade() = true
        monsArr.Add(foeName)
    ?monsArr ! ［］
      storage.Set(
        ^＂blade_used_monsters＂,
        ^string.Join(＂ ＂, monsArr)
      ^)

  breakString(10,foe.name,@nowMon)
  breakString(
    ^11,
    ^storage.Get(＂blade_used_monsters＂),
    ^＂monsNum＂ + string.Split(
      ^storage.Get(＂blade_used_monsters＂)
    ^)
    ^.Count() + ＂/35＂
  ^)
/** 完成400000重击伤害任务 **/
func debuffAtk()
  ? (!(foe &
  ^pickup.distance >= 20 &
  ^harvest.distance >= 20)) |
  ^(foe=boss & loc=deadwood & foe=phase2) |
  ^ hp <= maxhp * LOW_HP_PERSENT |
  ^ foe = immune_to_ranged |
  ^ loc = undead_crypt_boss_harder |
  ^ (loc=rocky & foe=boss &
  ^ foe.state = 115 & foe.time >= 60) |
  ^ ghostBossFlag = true |
  ^ needHealToMaxHp = true |
  ^ string.IndexOf(
    ^foe.buffs.string,
    ^＂buff_protection＂
  ^) ! -1
    return false

  var debuffStr = foe.debuffs.string
  ?string.IndexOf(
    ^debuffStr,
    ^＂debuff_damage:1＂
  ^) = -1&
    ^ foe ! immune_to_debuff_damage &
    ^ foe ! immune_to_ranged
    >`0,2,debuff poison
    equipL wand poison
    equipR shield ah
  :?string.IndexOf(
    ^debuffStr,
    ^＂debuff_chill:6＂
  ^) = -1&
    ^ foe ! immune_to_debuff_chill &
    ^ foe ! immune_to_ranged
    >`0,2,debuff ice
    equipL wand ice
    equipR shield ah
  :?string.IndexOf(
    ^debuffStr,
    ^＂debuff_dot:1＂
  ^) = -1&
    ^ foe ! immune_to_debuff_dot &
    ^ foe ! immune_to_ranged
    >`0,2,debuff fire
    equipL wand fire
    equipR shield ah
  :?string.IndexOf(
    ^debuffStr,
    ^＂stun:1＂
  ^) = -1 &
    ^ foe ! immune_to_stun &
    ^ foe ! immune_to_physical &
    ^ loc ! mine & foe ! explode
    >`0,2,debuff stun
    equipL hammer *10 +21
    equipR shield ah
/** 使用面具的主动能力 **/
func useMask()
  ?(foe = boss & loc = temple)
    return false

  ?item.GetCooldown(＂mask＂) <= 0 &
  ^foe.count > 0 &
  ^foe.distance <= 20 &
  ^pickup.distance >= 20 &
  ^harvest.distance >= 20
    equip mask
    ?item.CanActivate(＂mask＂)
      activate R
      >o-2,-3,#ffcccc,使用面具
      return true
  return false
/** 自动拾取
 *  距离可拾取物品足够近的时候
 *  自动拾取物品
**/
func autoPickup()
  ?pickup & pickup.distance < 10
    equipL star_stone
    equipR shield ah
/** 血量小于7喝药 **/
func autoPotion()
  ?hp <= maxhp*0.3
    activate potion
/** 加速
 *  在离怪物,可拾取物,可收获物
 *  足够远的时候装备三曲腿加速跑
**/
func runFast()
  ?foe.distance > 20 &
  ^ pickup.distance >= 20 &
  ^ harvest.distance >= 20
    equipL triskelion stone
    equipR shield ah

/** 岩石高地
*  自动铲石头
*  mainLO: 主Loadout
*  healLO: 治疗Loadout
**/
func levelRocky(mainLO,healLO)
  ?loc ! rocky
    return false
  AAC(
    ^＂sword ＂+getWeakness()+＂ D *10 +21＂,
    ^＂sword ＂+getWeakness()+＂ D *10 +20＂
  ^)
  // // 自动铲石头
  // ?harvest = Boulder &
  // ^harvest.distance < 30
  //   equip shovel
  ?foe = boss &
  ^getWeakness() ! false &
  ^string.IndexOf(
    ^foe.buffs.string,
    ^＂buff_protection＂
  ^) = -1
    AAC(
      ^＂sword ＂+getWeakness()+＂ D *10 +21＂,
      ^＂sword ＂+getWeakness()+＂ D *10 +20＂
    ^)
  ?foe.buffs.string
    adaptiveDef = foe.buffs.string
    adaptiveDef = string.Split(
                    ^adaptiveDef,
                    ^＂:＂,
                    ^true
                  ^)［1］
    adaptiveDef = string.Split(
                    ^adaptiveDef,
                    ^＂adaptive_defense_＂,
                    ^true
                  ^)［0］
  :
    adaptiveDef = ＂＂

  ?adaptiveDef = Poison
    AAC(
      ^＂sword Vigor D *10 +21＂,
      ^＂sword Vigor D *10 +20＂
    ^)
  :?adaptiveDef = Vigor
    AAC(
      ^＂sword AEther D *10 +21＂,
      ^＂sword AEther D *10 +20＂
    ^)
  :?adaptiveDef = AEther
    AAC(
      ^＂sword Fire D *10 +21＂,
      ^＂sword Fire D *10 +20＂
    ^)
  :?adaptiveDef = Fire
    AAC(
      ^＂sword Ice D *10 +21＂,
      ^＂sword Ice D *10 +20＂
    ^)
  :?adaptiveDef = Ice
    AAC(
      ^＂sword Poison D *10 +21＂,
      ^＂sword Poison D *10 +20＂
    ^)
  ?hp <= maxhp * LOW_HP_PERSENT
    loadout 0
  ?foe.state = 115 & foe.time >= 60
    equipL mind stone
/** 枯木峡谷
*  自动砍树
*  mainLO: 主Loadout
*  healLO: 治疗Loadout
*  phyLO: 物理伤害Loadout
**/
func levelDeadwood(mainLO,healLO,phyLO)
  ?loc ! deadwood
    return false
  AAC(
    ^＂sword ＂+getWeakness()+＂ D *10 +21＂,
    ^＂sword ＂+getWeakness()+＂ D *10 +20＂
  ^)
  loopHealHP(healLO)
  // 魔免怪加载近战
  ?foe = immune_to_ranged
    loadout @phyLO@
    ?ai.idle
      equipL wand ice *10 +21
      equip skeleton arm
  // 自动砍树
  ?harvest = Tree
    equip hatchet
  ?foe = wasp
    equipL wand ice
    equipR shield ah
  ?foe = boss & foe = phase2
    equipL war hammer *10 +21
    equipR sword Vigor *10 +21
/** 恐怖洞窟
*  在打完boss之后补满血
*  mainLO: 主Loadout
*  healLO: 治疗Loadout
**/
func levelCaves(mainLO,healLO)
  ?loc ! caves
    return false
  loadout @mainLO@
  loopHealHP(healLO)
  ?foe = tiny_spider
    equipWeaknessWand()
/** 蘑菇森林
*  mainLO: 主Loadout
*  healLO: 治疗Loadout
**/
func levelForest(mainLO,healLO)
  ?loc ! forest
    return false
  loadout @mainLO@
  loopHealHP(healLO)
  ?foe = ant
    equipWeaknessWand()
/** 亡者之殿
*  mainLO: 主Loadout
*  healLO: 治疗Loadout
*  magicLO: 魔法伤害Loadout
*  ? *>15 : use crossbow 3sec.
**/
func levelHalls(mainLO,healLO)
  ?loc ! halls
    return false
  loadout @mainLO@
  loopHealHP(healLO)
  ?foe = immune_to_physical
    equipL wand Vigor
    equipR shield ah
  /*
  如果是召唤boss,用3秒的冰驽

  如果还在打large_ghost,等7s,
  如果还是large_ghost,再来3s
  */
  ?foe = ghost_tomb &
  ^foe = boss &
  ^foe = spawner
    timer = 3*30
    ghostBossFlag = true

  ?timer > 0 & ghostBossFlag = true
    equip crossbow ice D *10 +21
    timer--
  :
    timer = 3*30
    ghostBossFlag = false
    ?foe = large_ghost
      ?timer2 > 0 & ghostBossFlag = false
        timer2--
      :
        timer2 = 7*30
        ?foe = large_ghost
          ghostBossFlag = true
  ?debug = true
    >`1,8,
    ^@getColor(ghostBossFlag)@,
    ^timer:@timer@,
    ^timer2:@timer2@

  ?loc = undead_crypt_boss_harder
    equipL wand Vigor
    equipR sword Vigor *10 +21

/** 灼热矿井
*  mainLO: 主Loadout
*  healLO: 治疗Loadout
**/
func levelMine(mainLO,healLO)
  ?loc ! mine
    return false
  equipWeaknessWand()
  ?foe = boss
    AAC(
      ^＂sword ＂+getWeakness()+＂ D *10 +21＂,
      ^＂sword ＂+getWeakness()+＂ D *10 +20＂
    ^)
  loopHealHP(healLO)
  ?(foe = fluff | foe = explode) &
  ^foe.distance <= 5
    equipL mind stone
/** 不融山
*  mainLO: 主Loadout
*  healLO: 治疗Loadout
**/
func levelIcy(mainLO,healLO)
  ?loc ! icy
    return false
  AAC(
    ^＂sword ＂+getWeakness()+＂ D *10 +21＂,
    ^＂sword ＂+getWeakness()+＂ D *10 +20＂
  ^)
  loopHealHP(healLO)
/** 神庙
*  mainLO: 主Loadout
*  healLO: 治疗Loadout
*  magicLO: 魔法伤害Loadout
**/
func levelTemple(mainLO,healLO,magicLO)
  ?loc ! temple
    return false
  AAC(
    ^＂sword ＂+getWeakness()+＂ D *10 +21＂,
    ^＂sword ＂+getWeakness()+＂ D *10 +20＂
  ^)
  loopHealHP(healLO)
  // 魔免怪加载近战
  ?foe = immune_to_Vigor
    loadout @magicLO@
  ?foe = flying_serpent
    equipWeaknessWand()
/** 显示宝箱位置 **/
func freeChest()
  ?loc = undead_crypt_intro
    freeChestTracker.Main()

/** 执行对应关卡的策略 **/
levelRocky(MAIN_LO,HEAL_LO)
levelDeadwood(MAIN_LO,HEAL_LO,PHY_LO)
levelCaves(MAIN_LO,HEAL_LO)
levelForest(MAIN_LO,HEAL_LO)
levelHalls(MAIN_LO,HEAL_LO)
levelMine(MAIN_LO,HEAL_LO)
levelIcy(MAIN_LO,HEAL_LO)
levelTemple(MAIN_LO,HEAL_LO,MAGIC_LO)
// 宝箱小游戏提示
freeChest()

/** 其他功能 **/
// 自动拾取
autoPickup()

//bladeMission35kill()
//addRstBtn()
debuffAtk()

// 偷取道具
steal()
// 堕神大剑相关
useBlade()

// 使用面具
useMask()
// 低血量回血
autoPotion()
// 加速跑
runFast()
// 显示对应的提示信息
showInfo()
/** 完毕 **/
