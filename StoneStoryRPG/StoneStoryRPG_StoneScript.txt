// 意念之石启动之后,可以持续执行它所收到的指令.
// 指令必须使用写入石之脚本.
// 其他信息请见:StoneStoryRPG.com/stonescript.
// ——贤者贝赛拉

//import Cosmetics/Hats/Skully
import Cosmetics/PetBoo
import Fishing
import UI/BetterInfo2

/** 预设变量 **/
?loc.begin
  var MAIN_LO = 2
  var HEAL_LO = 3
  var MAGIC_LO = 4
  var PHY_LO = 1

  var LOW_HP_PERSENT = 0.65
  var needHealToMaxHp = false

/** 在屏幕指定位置输出消息
 *  posY:屏幕Y轴坐标
 *  message 需要输出的消息,可以是变量名
**/
func Print(posY,message)
  >`1,@posY@,#ffc57f,@message@
/** 拆分字符串
 *  当信息超出一行的时候拆分成两行显示
**/
func breakString(posY,s,atS)
  ?s
    var a = string.Break(s, 60)
    ? a.Count() = 1
        >`1,@posY@,#ffc57f,@atS@:@a［0］@
    :
      for i = 0 .. a.Count()-1
        >`1,@posY+i@,#ffc57f,@atS@_@i+1@:@a［i］@
        //Print(posY+i, a［i］)

/** 岩石高地
 *  自动铲石头
 *  mainLO: 主Loadout
 *  healLO: 治疗Loadout
**/
func levelRocky(mainLO,healLO)
  ?loc = rocky
    loadout @mainLO@
    ?hp <= maxhp * LOW_HP_PERSENT
      loadout @healLO@
    // 自动铲石头
    ?harvest = Boulder &
    ^ harvest.distance < 30
      equip shovel
/** 枯木峡谷
 *  自动砍树
 *  mainLO: 主Loadout
 *  healLO: 治疗Loadout
 *  phyLO: 物理伤害Loadout
**/
func levelDeadwood(mainLO,healLO,phyLO)
  ?loc = deadwood
    loadout @mainLO@
    ?hp <= maxhp * LOW_HP_PERSENT
      loadout @healLO@
    // 魔免怪加载近战
    ?foe = immune_to_ranged
      loadout @phyLO@
    // 自动砍树
    ?harvest = Tree
      equip hatchet
/** 恐怖洞窟
 *  在打完boss之后补满血
 *  mainLO: 主Loadout
 *  healLO: 治疗Loadout
**/
func levelCaves(mainLO,healLO)
  ?loc = caves
    loadout @mainLO@
    ?foe = boss
      // loadout 4
      needHealToMaxHp = true
    ?hp <= maxhp * LOW_HP_PERSENT
      loadout @healLO@
    ?needHealToMaxHp = true & 
    ^hp <= maxhp &
    ^foe ! boss
      loadout @healLO@
      ?hp = maxhp
        needHealToMaxHp = false
    ?foe = tiny_spider
      loadout 4
/** 蘑菇森林
 *  mainLO: 主Loadout
 *  healLO: 治疗Loadout
**/
func levelForest(mainLO,healLO)
  ?loc = forest
    loadout @mainLO@
    ?hp <= maxhp * LOW_HP_PERSENT
      loadout @healLO@
    ?foe = ant
      loadout 4
/** 亡者之殿
 *  mainLO: 主Loadout
 *  healLO: 治疗Loadout
 *  magicLO: 魔法伤害Loadout
**/
func levelHalls(mainLO,healLO,magicLO)
  ?loc = halls
    loadout @mainLO@
    ?hp <= maxhp * LOW_HP_PERSENT
      loadout @healLO@
    ?foe = immune_to_physical
      loadout @magicLO@
/** 灼热矿井
 *  mainLO: 主Loadout
 *  healLO: 治疗Loadout
**/
func levelMine(mainLO,healLO)
  ?loc = mine
    loadout @mainLO@
    ?hp <= maxhp * LOW_HP_PERSENT
      loadout @healLO@
    ?foe = fluff & foe.distance = 5
      equipL mind stone
/** 不融山
 *  mainLO: 主Loadout
 *  healLO: 治疗Loadout
**/
func levelIcy(mainLO,healLO)
  ?loc = icy
    loadout @mainLO@
    ?hp <= maxhp * LOW_HP_PERSENT
      loadout @healLO@
/** 神庙
 *  mainLO: 主Loadout
 *  healLO: 治疗Loadout
 *  magicLO: 魔法伤害Loadout
**/
func levelTemple(mainLO,healLO,magicLO)
  ?loc = temple
    loadout @mainLO@
    ?hp <= maxhp * LOW_HP_PERSENT
      loadout @healLO@
      // 魔免怪加载近战
      ?foe = immune_to_Vigor
        loadout @magicLO@
      ?foe = flying_serpent
        loadout 4
/** 使用骷髅手臂
 *  在骷髅手臂可用,
 *  可以使用技能秒杀怪物
 *  非物理免疫,非boss
 *  不会干扰加速跑和自动捡取时
 *  装备并且使用骷髅手臂
**/
func steal()
  ?item.GetCooldown(＂skeleton_arm＂) <= 0 & 
  ^(foe.hp + foe.armor) < 20 &
  ^foe ! immune_to_physical & 
  ^foe ! boss &
  ^pickup.distance >= 20 &
  ^harvest.distance >= 20
    equip skeleton arm
    ?item.CanActivate(＂skeleton_arm＂)
       activate R
       >o-2,-3,#ffcccc,shhhh!~
/** 点击重置按钮之后重置
 *  blade_used_monsters
**/
func OnRstBtnPressed()
  var monsArr = 
  ^string.Split(
  ^storage.Get(＂blade_used_monsters＂))
  monsArr.RemoveAt(monsArr.IndexOf(＂青铜守卫＂))
  monsArr.RemoveAt(monsArr.IndexOf(＂火元素＂))

  storage.Set(＂blade_used_monsters＂, 
  ^string.Join(＂ ＂, monsArr))
  //storage.Delete(＂blade_used_monsters＂)

/** 添加重置按钮 **/
func addRstBtn()
  ?loc.begin
    var rstBtn = ui.AddButton()
    rstBtn.y = 1
    rstBtn.text = 删除指定元素
    rstBtn.SetPressed(OnRstBtnPressed)
/** 使用堕神大剑的主动能力 **/
func useBlade()
  ?item.GetCooldown(＂blade＂) <= 0 &
  ^foe.count > 5 &
  ^foe.distance <= 20 &
  ^pickup.distance >= 20 &
  ^harvest.distance >= 20 &
  ^(foe.armor + foe.hp) < 190
    equip blade
    ?item.CanActivate(＂blade＂)
      activate R
      >o-2,-3,#ffcccc,使用剑
      return true
  return false

/** 自动统计被堕神之剑的主动能力杀死的敌人
 *  用于完成 击败35类敌人 的任务
 *  由于无法获取全部敌人信息
 *  只能多次执行
**/
func bladeMission35kill()
  var monsArr
  var foeName
  monsArr = ［］
  foeName = foe.name

  ?foe &
  ^foe.distance <= 20 
    ?storage.Has(＂blade_used_monsters＂)
      monsArr = 
      ^string.Split(
      ^storage.Get(＂blade_used_monsters＂),true)
      ? ! monsArr.Contains(foeName)
        ?useBlade() = true
          monsArr.Add(foeName)
    :
      ?useBlade() = true
        monsArr.Add(foeName)
    ?monsArr ! ［］
      storage.Set(＂blade_used_monsters＂, 
      ^string.Join(＂ ＂, monsArr))
    
  breakString(10,foe.name,@nowMon)
  breakString(11,
  ^storage.Get(＂blade_used_monsters＂),
  ^＂monsNum＂ + string.Split(
  ^storage.Get(＂blade_used_monsters＂)).Count()
  ^ + ＂/35＂)
/** 完成400000重击伤害任务 **/
func bladeMission400kDMG()
  ?foe
    var debuffStr = foe.debuffs.string
    // >`0,2,foe.count:@foe.count@
    // breakString(3,debuffStr,＂debuffs＂)
    ?string.IndexOf(debuffStr,
      ^＂debuff_damage:1＂) = -1&
      ^ foe ! immune_to_debuff_damage &
      ^ foe ! immune_to_ranged
      equipL wand poison
      equipR shield ah
    :?string.IndexOf(
      ^debuffStr,＂debuff_chill＂) = -1&
      ^string.IndexOf(
      ^debuffStr,＂debuff_chill:6＂) = -1&
      ^ foe ! immune_to_debuff_damage &
      ^ foe ! immune_to_ranged
      equipL wand ice
      equipR shield ah
    :?string.IndexOf(
      ^debuffStr,＂debuff_dot:1＂) = -1&
      ^ foe ! immune_to_debuff_damage &
      ^ foe ! immune_to_ranged
      equipL wand fire
      equipR shield ah
    :?string.IndexOf(
      ^debuffStr,＂stun:1＂) = -1 &
      ^ foe ! immune_to_debuff_damage &
      ^ foe ! immune_to_stun
      equipL hammer *max +21
      equipR shield ah
    // ?foe.debuffs.string
    //   var debuffsArr = 
    //   ^string.Split(foe.debuffs.string,＂,＂,true)
    //   for i = 0 .. debuffsArr.Count()-1
    //     var debuffArr = 
    //     ^string.Split(debuffsArr［i］,＂:＂,true)
    //     var e = debuffArr［0］
    //     var eName = debuffArr［1］
    //     var eLevel = debuffArr［2］
    //     var eTime = debuffArr［3］
    //     breakString(10+i,eLevel + ＂ ＂ + eTime,eName)
    //     >`@i*23+1@,1,#ffc57f,＂element＂:@e@
    //     >`@i*23+1@,2,#ffc57f,＂eName＂:@eName@
    //     >`@i*23+1@,3,#ffc57f,＂eLevel＂:@eLevel@
    //     >`@i*23+1@,4,#ffc57f,＂eTime＂:@eTime@


/** 使用面具的主动能力 **/
func useMask()
  ?item.GetCooldown(＂mask＂) <= 0 &
  ^foe.count > 0 &
  ^foe.distance <= 20 &
  ^pickup.distance >= 20 &
  ^harvest.distance >= 20 &
  ^(foe ! boss & loc ! temple)
    equip mask
    ?item.CanActivate(＂mask＂)
      activate R
      >o-2,-3,#ffcccc,使用面具
      return true
  return false
/** 自动拾取
 *  距离可拾取物品足够近的时候
 *  自动拾取物品
**/
func autoPickup()
  ?pickup & pickup.distance < 10
    equipL star_stone
    equipR shield ah
/** 血量小于7喝药 **/
func autoPotion()
  ?hp <= maxhp*0.3
    activate potion
/** 加速
 *  在离怪物,可拾取物,可收获物
 *  足够远的时候装备三曲腿加速跑
**/
func runFast()
  ?foe.distance >= 20 &
  ^ pickup.distance >= 20 &
  ^ harvest.distance >= 20
    equipL triskelion fuse
    equipR shield ah
/** 显示自定义变量的值 **/
func showInfo()
  >c-42,11,Heal@maxhp * LOW_HP_PERSENT@:
  ^@hp <= maxhp * LOW_HP_PERSENT@
  ?loc = caves
    >c-42,10,NeedHealToMaxHp:
    ^@needHealToMaxHp@
/** 显示宝箱位置 **/
func freeChest()
  ?loc = undead_crypt_intro
    var freeChestTracker = 
    ^import UI/FreeChestTracker
    freeChestTracker.Main()
/** 执行对应关卡的策略 **/
levelRocky(MAIN_LO,HEAL_LO)
levelDeadwood(MAIN_LO,HEAL_LO,PHY_LO)
levelCaves(MAIN_LO,HEAL_LO)
levelForest(MAIN_LO,HEAL_LO)
levelHalls(MAIN_LO,HEAL_LO,MAGIC_LO)
levelMine(MAIN_LO,HEAL_LO)
levelIcy(MAIN_LO,HEAL_LO)
levelTemple(MAIN_LO,HEAL_LO,MAGIC_LO)
/** 其他功能
**/
// 自动拾取
autoPickup()
// 偷取道具
steal()
// 堕神大剑相关
useBlade()
//bladeMission35kill()
//addRstBtn()
bladeMission400kDMG()
// 使用面具
useMask()
// 低血量回血
autoPotion()
// 加速跑
runFast()
// 宝箱小游戏提示
freeChest()
// 显示对应的提示信息
showInfo()

//breakString(6,loc,loc.name)
//breakString(8,foe,foe.name)

/** 完毕 **/